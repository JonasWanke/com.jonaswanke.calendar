apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'calendar'

    publishedGroupId = 'com.jonaswanke.calendar'
    libraryName = 'Calendar'
    artifact = 'calendar'

    libraryDescription = 'Material Design CalendarView for Android'

    siteUrl = 'https://github.com/JonasWanke/com.jonaswanke.calendar'
    gitUrl = 'https://github.com/JonasWanke/com.jonaswanke.calendar.git'
    githubRepository = 'JonasWanke/com.jonaswanke.calendar'

    libraryVersion = rootProject.versionName

    developerId = 'JonasWanke'
    developerName = 'Jonas Wanke'
    developerEmail = 'contact@jonas-wanke.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}



publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/calendar-release.aar")
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses

        version {
            name = libraryVersion
            vcsTag = libraryVersion
        }
    }
    publications = ['Production']
}
